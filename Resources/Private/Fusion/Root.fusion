prototype(Neos.Neos:Page) {
	head {
		stylesheets.visibility  = Neos.Fusion:Tag {
			@if.liveAnfIncludeCSS = ${!node.context.inBackend && Configuration.setting('Dotpulse.Visibility.includeCSS')}
			@position = 'end'
			tagName = 'link'
			attributes {
				rel = 'stylesheet'
				href = Neos.Fusion:ResourceUri {
					path = 'resource://Dotpulse.Visibility/Public/Visibility.css'
				}
			}
		}
	}
}

prototype(Dotpulse.Visibility:ContentRawArray) < prototype(Neos.Fusion:RawArray) {
	hideOnXS = ${q(node).property('showOnXS') == false && !node.context.inBackend ? 'hidden-xs' : false}
	hideOnSM = ${q(node).property('showOnSM') == false && !node.context.inBackend ? 'hidden-sm' : false}
	hideOnMD = ${q(node).property('showOnMD') == false && !node.context.inBackend ? 'hidden-md' : false}
	hideOnLG = ${q(node).property('showOnLG') == false && !node.context.inBackend ? 'hidden-lg' : false}
	hideOnXL = ${q(node).property('showOnXL') == false && !node.context.inBackend ? 'hidden-xl' : false}
	hideOnPrint = ${q(node).property('showOnPrint') == false && !node.context.inBackend ? 'hidden-print' : false}
}

prototype(Dotpulse.Visibility:MenuRawArray) < prototype(Dotpulse.Visibility:ContentRawArray) {
	@context.node = ${item.node}
	hideOnPrint = false
}

prototype(Neos.Neos:Content) {
	@context.visibility = Dotpulse.Visibility:ContentRawArray

	attributes.class = Neos.Fusion:RawArray {
		visibility = ${Array.join(visibility, ' ')}
	}
}

/* Include Menu Classes like this:

prototype(Neos.Neos:Menu) {
	active.attributes.class = Dotpulse.Visibility:MenuRawArray {
		active = 'active'
	}

	current.attributes.class = Dotpulse.Visibility:MenuRawArray {
		current = 'active'
	}

	normal.attributes.class = Dotpulse.Visibility:MenuRawArray
}

*/
