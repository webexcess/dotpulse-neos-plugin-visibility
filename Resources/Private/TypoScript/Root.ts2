namespace: TypoScript=TYPO3.TypoScript
namespace: Neos=TYPO3.Neos
namespace: Visibility=Dotpulse.Visibility

prototype(Neos:Page) {
	head {
		stylesheets.visibility  = TypoScript:Tag {
			@if.liveAnfIncludeCSS = ${!node.context.inBackend && Configuration.setting('Dotpulse.Visibility.includeCSS')}
			@position = 'end'
			tagName = 'link'
			attributes {
				rel = 'stylesheet'
				href = TYPO3.TypoScript:ResourceUri {
					path = 'resource://Dotpulse.Visibility/Public/Visibility.css'
				}
			}
		}
	}
}

prototype(Visibility:ContentRawArray) < prototype(TypoScript:RawArray) {
	hideOnXS = ${q(node).property('showOnXS') == false && !node.context.inBackend ? 'hidden-xs' : false}
	hideOnSM = ${q(node).property('showOnSM') == false && !node.context.inBackend ? 'hidden-sm' : false}
	hideOnMD = ${q(node).property('showOnMD') == false && !node.context.inBackend ? 'hidden-md' : false}
	hideOnLG = ${q(node).property('showOnLG') == false && !node.context.inBackend ? 'hidden-lg' : false}
	hideOnXL = ${q(node).property('showOnXL') == false && !node.context.inBackend ? 'hidden-xl' : false}
	hideOnPrint = ${q(node).property('showOnPrint') == false && !node.context.inBackend ? 'hidden-print' : false}
}

prototype(Visibility:MenuRawArray) < prototype(Visibility:ContentRawArray) {
	@context.node = ${item.node}
	hideOnPrint = false
}

prototype(Neos:Content) {
	@context.visibility = Visibility:ContentRawArray

	attributes.class = TypoScript:RawArray {
		visibility = ${Array.join(visibility, ' ')}
	}
}

/* Include Menu Classes like this:

prototype(Neos:Menu) {
	active.attributes.class = Dotpulse.Visibility:MenuRawArray {
		active = 'active'
	}

	current.attributes.class = Dotpulse.Visibility:MenuRawArray {
		current = 'active'
	}

	normal.attributes.class = Dotpulse.Visibility:MenuRawArray
}

*/
